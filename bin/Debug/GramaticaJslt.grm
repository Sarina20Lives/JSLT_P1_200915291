!----------------------------------------------------------------------------!
!   EXPRESIONES REGULARES
!----------------------------------------------------------------------------!

{str}       =   {Printable} - ["]
{ctr}       =   {Printable} - [''] 
{alpha}     =   {AlphaNumeric} + [_]            
            
!----------------------------------------------------------------------------!
!   TERMINALES 
!----------------------------------------------------------------------------!

STRING      = '"' {str}* '"'
CHAR        = '' {ctr} ''
INT         = {Digit}+
DOUBLE      = {Digit}+ '.' {Digit}+  
ID          = {Letter}{alpha}*       

Comment Start = '#*'
Comment End   = '*#'
Comment Line  = '##'        
                                    
!----------------------------------------------------------------------------!
!   CONFIGURACIONES
!----------------------------------------------------------------------------!

"Case Sensitive" = 'False'
"Start Symbol"  = <lenguaje>

!----------------------------------------------------------------------------!
!   GRAMATICA 
!----------------------------------------------------------------------------!

<lenguaje>      ::=     <json>
                 |      <jslt>

!----------------------------------------------------------------------------!
!   GRAMATICA - JSON
!----------------------------------------------------------------------------!

<json>          ::=     '{' STRING ':' '{' <objsJS> '}' '}'

<objsJS>        ::=     <objsJS> ',' <objJS>
                 |      <objJS> 
                 
<objJS>         ::=     STRING ':' '[' <llasJS> ']'
                 
<llasJS>        ::=     <llasJS> ',' <llaJS>
                 |      <llaJS>    
                     
<llaJS>         ::=     '{' <atbsJS> '}'
                 
<atbsJS>        ::=     <atbsJS> ',' <atbJS>
                 |      <atbJS>
             
<atbJS>         ::=     STRING ':' STRING
       
!----------------------------------------------------------------------------!
!   GRAMATICA - JSLT
!----------------------------------------------------------------------------!

<jslt>          ::=     '<jsl:transformacion' 'ruta' '=' STRING 'version' '=' STRING '>' <cuerpo> '</jsl:final>'

<cuerpo>        ::=     <declaracion> <cuerpo> 
                 |      <plantilla> <cuerpo> 
                 |                       

<sentencias>    ::=     <sentencia> <sentencias>
                 |     
                 
<sentencia>     ::=     <declaracion>
                 |      <asignacion>    
                 |      <aplantilla>
                 |      <getval>
                 |      <bfor>
                 |      <fif>
                 |      <fswitch>
                 |      <etqhtml>
           
!----------------------------------------------------------------------------!
!   Declaracion de variables

<declaracion>   ::=     '<jsl:variable' <tipo> '=' ID '/>'                

<tipo>          ::=     'entero'
                 |      'cadena'
                 |      'boolean'
                 |      'doble'
                 |      'caracter'

!----------------------------------------------------------------------------!
!   Asignacion de variables

<asignacion>    ::=     '<jsl:asignar' 'variable' '=' ID 'valor' <valAsig> '/>'
             
<valAsig>       ::=     '=' <expresion>
                 |      '+=' <expresion>
                 |      '++'
                 |      '--'
                                                                     
!----------------------------------------------------------------------------!
!   Declaracion de plantillas

<plantilla>     ::=     '<jsl:plantilla' 'nombreObj' '=' <obj> '>' <sentencias> '</jsl:plantilla>'

!----------------------------------------------------------------------------!
!   Aplicar Plantilla

<aplantilla>    ::=     '<jsl:plantilla-aplicar' 'seleccionar' '=' <obj> '/>'
               
!----------------------------------------------------------------------------!
!   Obtener el valor de un objeto   
               
<getval>        ::=     '<jsl:valor-de' 'seleccionar' '=' <obj> '/>'

!----------------------------------------------------------------------------!
!   Bucle Para Cada                

<bfor>          ::=     '<jsl:para-cada' 'seleccionar' '=' <obj> '>' <sentencias> '</jsl:para-cada>'

!----------------------------------------------------------------------------!
!   Flujo If                  

<fif>           ::=     '<jsl:if' 'condicion' '=' <expresion> '>' <sentencias> '</jsl:if>'

!----------------------------------------------------------------------------!
!   Flujo Switch                

<fswitch>       ::=     '<jsl:en-caso>' <casos> '</jsl:en-caso>'

<casos>         ::=     <caso> <casos>
                 |      <defecto>
                 |      <caso>

<caso>          ::=     '<jsl:de' 'condicion' '=' <expresion> '>' <sentencias> '</jsl:de>'

<defecto>       ::=     '<jsl:cualquier-otro>' <sentencias> '</jsl:cualquier-otro>'       
                 
!----------------------------------------------------------------------------!
!   Objetos

<obj>           ::=     <var>
                 |      '@@' ID    
                 |      '@' ID <ruta>
                 |      '@'
                 |      '..'
                 |      '.'

<ruta>          ::=     '@' ID <ruta>
                 |      '@' ID '[' <expresion> ']' 
                 |                      

<var>           ::=     ID '[' <expresion> ']'       
                 |      ID                 

!----------------------------------------------------------------------------!
!   Expresion

<expresion>     ::=     <log1>
                 
<log1>          ::=     <log1> '||' <log2>
                 |      <log1> '!||' <log2>
                 |      <log1> '&|' <log2>
                 |      <log2>

<log2>          ::=     <log2> '&&' <log3>
                 |      <log2> '!&&' <log3>
                 |      <log3>
                 
<log3>          ::=     '!' <rel>
                 |      <rel>
                 
<rel>           ::=     <rel> '==' <arit1>
                 |      <rel> '!=' <arit1>
                 |      <rel> '&lt' <arit1>
                 |      <rel> '&lte' <arit1>
                 |      <rel> '&gt' <arit1>
                 |      <rel> '&gte' <arit1>
                 |      <arit1>

<arit1>         ::=     <arit1> '+' <arit2>
                 |      <arit1> '-' <arit2>
                 |      <arit2>                 

<arit2>         ::=     <arit2> '*' <arit3>
                 |      <arit2> 'div' <arit3>
                 |      <arit2> '%' <arit3>
                 |      <arit3>                 

<arit3>         ::=     <arit3> '^' <unitario>
                 |      <unitario>                 

<unitario>      ::=     <getval>
                 |      '!¡' ID
                 |      '++' ID 
                 |      '--' ID 
                 |      ID '++'
                 |      ID '--'
                 |      <val>

<val>           ::=     '(' <log1> ')'
                 |      <obj>
                 |      CHAR
                 |      STRING
                 |      '-' <numero> 
                 |      <numero>
                 |      'true'
                 |      'false'              
                 
<numero>        ::=     INT
                 |      DOUBLE
                 
!----------------------------------------------------------------------------!
!   GRAMATICA - HTML
!----------------------------------------------------------------------------!

<etqhtml>       ::=     '<html>' <sentencias> '</html>' 
                 |      '<body>' <sentencias> '</body>'
                 |      '<head>' <titulo> '</head>'
                 |      '<h1>' <sentencias> '</h1>' 
                 |      '<h2>' <sentencias> '</h2>' 
                 |      '<h3>' <sentencias> '</h3>' 
                 |      '<h4>' <sentencias> '</h4>' 
                 |      '<h5>' <sentencias> '</h5>' 
                 |      '<h6>' <sentencias> '</h6>'
                 |      '<table' <propers> <filas> '</table>'
                 |      '<p>' <sentencias> '</p>'
                 |      '<b>' <sentencias> '</b>'
                 |      '<i>' <sentencias> '</i>'

<titulo>        ::=     '<title>' <sentencias> '</title>'
                 |      <sentencias>
                 
<filas>         ::=     <fila> <filas>
                 |      

<fila>          ::=     '<tr' <propers> <columns> '</tr>'

<columns>       ::=     <column> <columns>
                 |                      

<column>        ::=     '<th' <propers> <sentencias> '</th>'
                 |      '<td' <propers> <sentencias> '</td>'                      

<propers>       ::=     <proper> <propers>     
                 |      '>'                 

<proper>        ::=     'border' '=' STRING
                 |      'bgcolor' '=' STRING
                 |      'width' '=' STRING
                 |      'height' '=' STRING
